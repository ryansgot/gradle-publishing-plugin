buildscript {
    ext.keys = [
            'aws': [
                    'accessKeyId': project.hasProperty("awsMavenAccessKey") ? project.property("awsMavenAccessKey") : "${System.env.AWS_ACCES_KEY_ID}",
                    'secretKey': project.hasProperty("awsMavenSecretKey") ? project.property("awsMavenSecretKey") : "${System.env.AWS_SECRET_KEY}",
            ],
            'bintray': [
                    'user': project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : '',
                    'apiKey': project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : ''
            ]
    ]

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5"
    }
}

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'com.fsryan.gradle'
version '0.1.2'

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile 'junit:junit:4.12'
}

def siteUrl = 'https://github.com/ryansgot/gradle-publishing-plugin'
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id 'fsryan'
            name 'Ryan Scott'
            email 'fsryan.developer@gmail.com'
        }
    }
    scm {
        connection "${siteUrl}.git"
        developerConnection "${siteUrl}.git"
        url siteUrl
    }
}

publishing {
    repositories {
        maven {
            name 'release'
            url 's3://repo.fsryan.com/release'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
        maven {
            name 'snapshot'
            url 's3://repo.fsryan.com/snapshot'
            credentials(AwsCredentials) {
                accessKey = keys.aws.accessKeyId
                secretKey = keys.aws.secretKey
            }
        }
    }
    publications {
        plugin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'fsryan-gradle-publishing'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin providing tasks for publishing libraries to a maven repository')
                root.appendNode('name', 'fsryan-gradle-publishing')
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = project.hasProperty("bintrayUser") ? project.property("bintrayUser").toString() : ""
    key = project.hasProperty("bintrayApiKey") ? project.property("bintrayApiKey").toString() : ""
    publications = ["plugin"]
    publish = false

    pkg {
        repo = "maven"
        name = project.name
        desc = "Gradle plugin providing tasks for publishing libraries to a maven repository"
        websiteUrl = siteUrl
        issueTrackerUrl = "$siteUrl/issues"
        vcsUrl = "${siteUrl}.git"
        publicDownloadNumbers = true
        licenses = ['Apache-2.0']
        labels = ['gradle', 'publishing', 'android', 'aar']
        version {
            name = project.version.toString()
            vcsTag = "v${project.version}"
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.groovy.srcDirs
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

afterEvaluate {
    def releaseTask = project.task("release", dependsOn: ["bintrayUpload", "publishPluginPublicationToReleaseRepository"])
    releaseTask.description = "Release to bintray and S3"
    releaseTask.group = "Publishing"
}